// seven_four.fe

// Evolver data for Seifert surface with boundary the 7_4 knot
// the curve is given by a simple Fourier series
// i do not yet know if the knot is chiral -- will check

PARAMETER  RHO = 0.4   // measure of vertical size; could add more parameters

boundary 1 parameters 1     //  boundary curve
x1: sin(3*p1) 
x2: sin(2*p1) 
x3: RHO * cos(7*p1) 

vertices   // given in terms of boundary parameter
1     0.00  boundary 1   fixed
2     pi/12 boundary 1   fixed
3     pi/6  boundary 1   fixed
4     pi/4  boundary 1   fixed
5     pi/3  boundary 1   fixed
6   5*pi/12 boundary 1   fixed
7     pi/2  boundary 1   fixed
8   7*pi/12 boundary 1   fixed
9   2*pi/3  boundary 1   fixed
10  3*pi/4  boundary 1   fixed
11  5*pi/6  boundary 1   fixed
12 11*pi/12 boundary 1   fixed
13    pi    boundary 1   fixed
14 13*pi/12 boundary 1   fixed
15  7*pi/6  boundary 1   fixed
16  5*pi/4  boundary 1   fixed
17  4*pi/3  boundary 1   fixed
18 17*pi/12 boundary 1   fixed
19  3*pi/2  boundary 1   fixed 
20 19*pi/12 boundary 1   fixed
21  5*pi/3  boundary 1   fixed
22 21*pi/12 boundary 1   fixed 
23 11*pi/6  boundary 1   fixed
24 23*pi/12 boundary 1   fixed  

edges
1    1  2  boundary 1   fixed
2    2  3  boundary 1   fixed
3    3  4  boundary 1   fixed
4    4  5  boundary 1   fixed
5    5  6  boundary 1   fixed
6    6  7  boundary 1   fixed
7    7  8  boundary 1   fixed
8    8  9  boundary 1   fixed
9    9  10 boundary 1   fixed
10   10 11 boundary 1   fixed
11   11 12 boundary 1   fixed
12   12 13 boundary 1   fixed
13   13 14 boundary 1   fixed
14   14 15 boundary 1   fixed
15   15 16 boundary 1   fixed
16   16 17 boundary 1   fixed
17   17 18 boundary 1   fixed
18   18 19 boundary 1   fixed
19   19 20 boundary 1   fixed
20   20 21 boundary 1   fixed
21   21 22 boundary 1   fixed
22   22 23 boundary 1   fixed
23   23 24 boundary 1   fixed
24   24 1  boundary 1   fixed
25   1  13 
26   2  18 
27   5  17
28   6  14 
29   8  24 
30   9  21 
31   12 20 

faces
1   1 26 18 19 -31 12 -25    
2   2 3 4 27 17 -26 
3   5 28 14 15 16 -27 
4   6 7 29 24 25 13 -28
5   8 30 21 22 23 -29 
6   9 10 11 31 20 -30  

read

// simple set of commands to get things going
gogo := { r; u; r; g 2; u 2; g 2; t 0.08; u 2; g 5; u 2; }

// run command
go100 := { g 100; u 3; t 0.03; u 3 }
